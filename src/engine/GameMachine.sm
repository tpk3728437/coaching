// -*- tab-width: 4; -*-

%{
// GameMachineStateMachine
%}

%class HeadsOrTailsGame
%header   HeadsOrTailsGame.h
%include "globals.h"
%start MainMap::StartState

%map MainMap
%%
// State        Transition      End State           Action(s)
StartState
Entry { playStarted(); flipCoin(); }
{
                head            HeadState           { coinFlipped(Heads); }
                tail            TailState           { coinFlipped(Tails); }
}

HeadState
Entry { flipCoin(); }
{
                head            HeadHeadState       { coinFlipped(Heads); }
                tail            HeadTailState       { coinFlipped(Tails); }
}

TailState
Entry { flipCoin(); }
{
                head            TailHeadState       { coinFlipped(Heads); }
                tail            LoseState           { coinFlipped(Tails); }
}

HeadHeadState
Entry { flipCoin(); }
{
                head            BigWinState         { coinFlipped(Heads); }
                tail            WinState            { coinFlipped(Tails); }
}

HeadTailState
Entry { flipCoin(); }
{
                head            WinState            { coinFlipped(Heads); }
                tail            LoseState           { coinFlipped(Tails); }
}

TailHeadState
Entry { flipCoin(); }
{
                head            WinState            { coinFlipped(Heads); }
                tail            LoseState           { coinFlipped(Tails); }
}

WinState
Entry { gameWin(); doesUserWantToDoubleUp(); }
{
                doubleUp        DoubleUpState       {}
                quit            GameEndState        {}
}

LoseState
Entry { gameLoss(); }
{
                quit            GameEndState        {}      
}

BigWinState
Entry { bigWin(); }
{
                quit            GameEndState        {}
}

DoubleUpState
Entry { flipCoin(); }
{                
                head            WinState            { coinFlipped(Heads);  doubleUpWin(true); }
                tail            LoseState           { coinFlipped(Tails);  doubleUpWin(false); }
}

GameEndState
Entry { gameEnd(); }
{
                restart         StartState          { reset(); }
}

%%
